ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 0;
gInitialMainCB2 = CB2_TestRunner;

SECTIONS {
    . = 0x2000000;

    ewram (NOLOAD) :
    ALIGN(4)
    {
        gHeap = .;

        . = 0x1C000;

        src/*.o(ewram_data);
        gflib/*.o(ewram_data);
        test/*.o(ewram_data);

        . = 0x40000;
    }

    . = 0x3000000;

    iwram (NOLOAD) :
    ALIGN(4)
    {
        /* .bss starts at 0x3000000 */
        src/*.o(.bss);
        gflib/*.o(.bss);
        data/*.o(.bss);
        test/*.o(.bss);
        *libc.a:*.o(.bss*);
        *libgcc.a:*.o(.bss*);
        *libnosys.a:*.o(.bss*);

        /* .bss.code starts at 0x3001AA8 */
        src/m4a.o(.bss.code);

        /* COMMON starts at 0x30022A8 */
        src/*.o(COMMON);
        gflib/*.o(COMMON);
        data/*.o(COMMON);
        test/*.o(COMMON);
        *libc.a:sbrkr.o(COMMON);
        end = .;

	/* .persistent starts at 0x3007F00 */
	/* WARNING: This is the end of the IRQ stack, if there's too
	 * much data it WILL be overwritten. */
	. = 0x7F00;
	test/*.o(.persistent);

        . = 0x8000;
    }

    . = 0x8000000;

    .text :
    ALIGN(4)
    {
        src/rom_header.o(.text);
        src/rom_header_gf.o(.text.*);
        src/*.o(.text);
        gflib/*.o(.text);
    } =0

    script_data :
    ALIGN(4)
    {
	data/*.o(script_data);
    } =0

    lib_text :
    ALIGN(4)
    {
        *libagbsyscall.a:*.o(.text*);
        *libgcc.a:*.o(.text*);
        *libc.a:*.o(.text*);
        *libnosys.a:*.o(.text*);
    } =0

    .rodata :
    ALIGN(4)
    {
        src/*.o(.rodata*);
        gflib/*.o(.rodata*);
        data/*.o(.rodata*);
    } =0

    song_data :
    ALIGN(4)
    {
        sound/songs/*.o(.rodata);
    } =0

    lib_rodata :
    SUBALIGN(4)
    {
        *libgcc.a:*.o(.rodata*);
        *libc.a:*.o(.rodata*);
        *libc.a:*.o(.data*);
        src/libisagbprn.o(.rodata);
    } =0

    tests :
    ALIGN(4)
    {
        __start_tests = .;
        test/*.o(.tests);
        __stop_tests = .;
        test/*.o(.text);
        test/*.o(.rodata*);
    } =0

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
